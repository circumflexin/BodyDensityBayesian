


## declare variables

var 
    
    compr, CdAM.g, CdAM.var, CdAM[NW], body.density.g, Vair, Vair.var, Vair.d[ND];  #, tau[NW]
    
    model{
    
    ## CONSTANTS
    
    g <- 9.80665
    pair <- 1.225 # air density at sea level, kg/m3
    atmPa <- 101325 # conversion from pressure in atmospheres into Pascals
    
    ## PRIORS
    
    ## global parameters
    
      # compressibility      
      compr ~ dunif(0.1, 0.7) # x10^(-9) Pa^(-1)

      # mass-specific volume of air
      Vair ~ dunif(5, 80)
      Vair.SD ~ dunif(1e-06, 200)
      Vair.var <- Vair.SD^2
  
      # CdAM
      CdAM.g ~ dunif(1e-06, 30)         
      CdAM.SD ~ dunif(1e-06, 200)
      CdAM.var <- CdAM.SD^2
  
      # body density
      body.density.g ~ dunif(800, 1200)
    

    ## individual-specific parameters
    
    for(w in 1:NW) { # loop across individuals

  
      # CdAM
      CdAM.shape[w] <- (CdAM.g^2)/CdAM.var 
      CdAM.rate[w] <- CdAM.g/CdAM.var
      CdAM[w] ~ dgamma(CdAM.shape[w], CdAM.rate[w])T(1e-06,)
    
    
    }
    
    ## dive-specific parameters
    
    for(d in 1:ND) { # loop across dives
    
    Vair.shape[d] <- (Vair^2)/Vair.var 
    Vair.rate[d] <- Vair/Vair.var
    Vair.d[d] ~ dgamma(Vair.shape[d], Vair.rate[d])T(1e-06,)
    
    }
    
    
    
    ## THE MODEL
    
    for(j in 1:N) { # loop across data
    
    term1[j] <- -0.5*CdAM[whale.id[j]]/(10^6)*DswGG[j]*mean.speed[j]^2
    
    body.density.t[j] <- body.density.g/(1-compr*(1+0.1*depth[j])*atmPa*10^(-9))
    
    term2[j] <- (DswGG[j]/body.density.t[j]-1)*g*sin.pitch[j]
    
    term3[j] <- Vair.d[dive.id[j]]/1000000*g*sin.pitch[j]*(DswGG[j]-pair*(1 + 0.1*depth[j]))*1/(1 + 0.1*depth[j])  
    
    a.mu[j] <- term1[j] + term2[j] + term3[j]
    
    a[j] ~ dnorm(a.mu[j], tau[j])
    
    } 
    
    }